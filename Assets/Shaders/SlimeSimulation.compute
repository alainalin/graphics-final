#pragma kernel UpdateSlimeSimulation

// redefine necessary structs
struct SlimeAgent {
    float2 position;
    float angle; // direction agent faces in radians
    int speciesID; // identifying index into species buffer 
};

struct SpeciesSettings {
    float sensorAngle;
    float rotationAngle; 
    // should be bigger than sensorAngle to avoid convergence, adjusted by random offset (ranging 0, 45)? in compute shader 

    int sensorDist; 
    int sensorRadius;
    float velocity;
    float trailWeight;
    float4 color;
};

RWTexture2D<float4> TrailMap;
RWTexture2D<float4> NextTrailMap;

int width;
int height;

RWStructuredBuffer<SlimeAgent> slimeAgents;
uint numAgents;

RWStructuredBuffer<SpeciesSettings> species;

float decayRate;
float diffuseRate;

float dt;
float time;

float4 speciesMask(int speciesID) {
    return float4(speciesID == 0, speciesID == 1, speciesID == 2, speciesID == 4);
}

float clamp(float num, float lower, float upper)
{
    return max(lower, min(num, upper));
}

// Hash function www.cs.ubc.ca/~rbridson/docs/schechter-sca08-turbulence.pdf
uint hash(uint state)
{
    state ^= 2747636419u;
    state *= 2654435769u;
    state ^= state >> 16;
    state *= 2654435769u;
    state ^= state >> 16;
    state *= 2654435769u;
    return state;
}

float scaleToRange01(uint state)
{
    return state / 4294967295.0;
}

// sample trails around agent where angleOffset is the offset from the original direction that the agent is traveling in 
// returning the strength of attraction of the agent towards the offseted angle 
float sampleTrail(SlimeAgent agent, float angleOffset) 
{
    SpeciesSettings agentSettings = species[agent.speciesID];
    float2 dir = float2(cos(agent.angle + angleOffset), sin(agent.angle + angleOffset));
    int2 pos = int2(agent.position + dir * agentSettings.sensorDist);

    int rad = agentSettings.sensorRadius;

    float4 strength = 0; 
    for (int x = -rad; x < rad + 1; x++) {
        for (int y = -rad; y < rad + 1; y++) {
            int xPos = clamp(pos.x + x, 0, width - 1);
            int yPos = clamp(pos.y + y, 0, height - 1);

            strength += TrailMap[int2(xPos, yPos)];
        }
    }

    int agentID = agent.speciesID;
    float4 attractions = 2 * speciesMask(agent.speciesID) - 1;

    return dot(strength, attractions);
} 

void step(int agentIdx) {
    SlimeAgent agent = slimeAgents[agentIdx];
    SpeciesSettings agentSettings = species[agent.speciesID];
    
    float2 dir = float2(cos(agent.angle), sin(agent.angle));
    float2 newPos = agent.position + agentSettings.velocity * dir * dt;

    int rand = hash(hash(agent.position.y * width + agent.position.x + hash(agentIdx + time * 100000)));

    if (newPos.x < 0 || newPos.x >= width || newPos.y < 0 || newPos.y >= height)
    {
        newPos.x = clamp(newPos.x, 0, width-1);
        newPos.y = clamp(newPos.y, 0, height-1);
        slimeAgents[agentIdx].angle = scaleToRange01(rand) * 2 * 3.14159;
    }

    int2 pxlXY = int2(newPos);

    // deposit slime
    float4 agentMask = speciesMask(agent.speciesID);
    TrailMap[pxlXY] = min(1, TrailMap[pxlXY] + agentMask * agentSettings.trailWeight * dt);

    slimeAgents[agentIdx].position = newPos;
}

[numthreads(16,1,1)]
void UpdateSlimeSimulation (uint3 id : SV_DispatchThreadID)
{
    // TODO: update the state of slime agents based on the trail map
    if (id.x >= numAgents) 
    {
        return;
    }

    SlimeAgent agent = slimeAgents[id.x];
    SpeciesSettings agentSettings = species[agent.speciesID];

    // using same hashing method as Sebastian here
    int rand = hash(agent.position.y * width + agent.position.x + hash(id.x + time * 100000));

    float leftSample = sampleTrail(agent, agentSettings.sensorAngle);
    float midSample = sampleTrail(agent, 0);
    float rightSample = sampleTrail(agent, -agentSettings.sensorAngle);

    float turnWeight = scaleToRange01(rand);
    float turnAngle = agentSettings.rotationAngle;

    if ((midSample > leftSample) && (midSample > rightSample)) {
        // forward
        slimeAgents[id.x].angle += 0;
    } else if ((midSample < leftSample) && (midSample < rightSample)) {
        // random
        slimeAgents[id.x].angle += 2 * (turnWeight - 0.5) * turnAngle * dt;
    } else if (leftSample > rightSample) {
        // left
        slimeAgents[id.x].angle += turnWeight * turnAngle * dt;
    } else if (rightSample > leftSample) {
        // right
        slimeAgents[id.x].angle -= turnWeight * turnAngle * dt;
    } else {
        slimeAgents[id.x].angle += 0;
    }
    
    step(id.x);
}

#pragma kernel Blur

[numthreads(8,8,1)]
void Blur (uint3 id : SV_DispatchThreadID)
{
    float4 prevPxl = TrailMap[id.xy];
    float4 outPxl = float4(0, 0, 0, 0);
    
    for (int i = -1; i < 2; i++) {
        for (int j = -1; j < 2; j++) {
            int x = clamp(id.x + i, 0, width-1);
            int y = clamp(id.y + j, 0, height-1);
            outPxl += TrailMap[int2(x,y)];
        }
    }

    outPxl = outPxl / 9;
    outPxl = prevPxl * (1 - diffuseRate * dt) + outPxl * diffuseRate * dt;
    NextTrailMap[id.xy] = max(0, outPxl - decayRate * dt);
}

#pragma kernel PaintSlimeSimulation

RWTexture2D<float4> ViewportTex;
RWTexture2D<float4> FoodMap;
float4 foodColor;

[numthreads(8,8,1)]
void PaintSlimeSimulation (uint3 id : SV_DispatchThreadID)
{
    ViewportTex[id.xy] = (TrailMap[id.xy].x * species[0].color) + (FoodMap[id.xy] * foodColor);
}

#pragma kernel PaintFood

int2 clickPos;
int foodBrushRadius;

[numthreads(8,8,1)]
void PaintFood (uint3 id : SV_DispatchThreadID)
{
    if (length(id.xy - clickPos) <= foodBrushRadius) {
        FoodMap[id.xy] = foodColor;
    }
}

#pragma kernel ClearTextures

[numthreads(8,8,1)]
void ClearTextures (uint3 id : SV_DispatchThreadID)
{
    TrailMap[id.xy] = float4(0.0, 0.0, 0.0, 0.0);
    FoodMap[id.xy] = float4(0.0, 0.0, 0.0, 0.0);
}
